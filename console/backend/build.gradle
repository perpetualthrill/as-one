plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.31'
    id 'com.github.johnrengelman.shadow' version '5.0.0'
    id 'org.jetbrains.kotlin.kapt' version '1.3.31'
    id 'application'
    // provides 'gradle checkUpdates'
    id 'name.remal.check-updates' version '1.0.144'
}

application {
    mainClassName = 'org.perpetualthrill.asone.console.ConsoleStatic'
}

group 'org.perpetualthrill.asone.console'
version '0.1'

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://jitpack.io' }
}

// Thanks, IDEA. How come the preprocessor runs automagically in Android
// Studio but not here?
task rebuildAndRun {
    dependsOn 'classes'
    dependsOn 'run'
    tasks.findByName('run').mustRunAfter 'classes'
}

// Remove the dang experimental warning. Ignore error underlining here -- who even knows 
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions.freeCompilerArgs += ["-Xuse-experimental=io.ktor.locations.KtorExperimentalLocationsAPI"]
}

// get current git hash, i.e. to reproduce the build
def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

// Add git commit to build for repeatability purposes
shadowJar {
    archiveAppendix = getGitHash()
}

configurations.all {
    // Force out alien slf4j implementation
    exclude group: 'org.slf4j', module: 'slf4j-log4j12'

    // Force out any log4j imports in favor of slf4j
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.name == 'log4j') {
            details.useTarget "org.slf4j:log4j-over-slf4j:1.7.5"
        }
    }
}

ext {
    daggerVersion = '2.23.1'
    ktorVersion = '1.2.1'
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.9'
    implementation 'com.fazecast:jSerialComm:2.5.1'
    implementation 'ch.qos.logback:logback-classic:1.2.3' // secretly this is also slf4j
    implementation 'org.xbib.jdbc:jdbc-driver-csv:1.2.0'
    implementation 'com.github.sylvek:embedded-mosquitto:-SNAPSHOT'
    implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.1'
    implementation 'com.github.ajalt:clikt:2.0.0'

    implementation "io.ktor:ktor-server-core:$ktorVersion"
    implementation "io.ktor:ktor-server-netty:$ktorVersion"
    implementation "io.ktor:ktor-gson:$ktorVersion"
    implementation "io.ktor:ktor-locations:$ktorVersion"

    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
}
